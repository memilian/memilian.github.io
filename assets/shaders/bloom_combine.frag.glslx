#ifdef GL_ES
precision mediump float;
#endif

uniform sampler2D tex0;
uniform sampler2D baseSampler;
uniform float bloomIntensity;
uniform float baseIntensity;
uniform float bloomSaturation;
uniform float baseSaturation;

varying vec2 tcoord;
varying vec4 color;

// Helper for modifying the saturation of a color.
vec4 adjustSaturation(vec4 color, float saturation)
{
    // The constants 0.3, 0.59, and 0.11 are chosen because the
    // human eye is more sensitive to green light, and less to blue.
    float grey = dot(color.rgb, vec3(0.3, 0.59, 0.11));

    return mix(vec4(grey), color, saturation);
}

void main() {
    // Look up the bloom and original base image colors.
    vec4 bloom = texture2D(tex0, tcoord);
    vec4 base = texture2D(baseSampler, tcoord);

    // Adjust color saturation and intensity.
    bloom = adjustSaturation(bloom, bloomSaturation) * bloomIntensity;
    base = adjustSaturation(base, baseSaturation) * baseIntensity;

    // Darken down the base image in areas where there is a lot of bloom,
    // to prevent things looking excessively burned-out.
    base *= (1.0 - clamp(bloom, 0.0, 1.0));

    // Combine the two images.
    gl_FragColor = vec4(base.rgb + bloom.rgb, 1.0);
}
